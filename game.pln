import graphics
import math
sdl = graphics

# print = { takes text to }

keyboard = [sdl.SDL_SCANCODE_UP: false, sdl.SDL_SCANCODE_DOWN: false, sdl.SDL_SCANCODE_SPACE: false]

SCREEN_WIDTH = 1000
SCREEN_HEIGHT = 800

Ball = class {
    @init = { takes x, y, texture to
        self.x = x
        self.y = y
        self.dx = 0
        self.dy = 0
        self.texture = texture
        
        details = []
        sdl.query_texture(texture, details)
        self.width = details["w"] / 10
        self.height = details["h"] / 10
        
        self._bounces = 0 # debugging
    }
    
    debug_print = {
        print("--- Ball ---")
        print("X: " + to_string(self.x) + ", Y: " + to_string(self.y))
        print("DX: " + to_string(self.dx) + ", DY: " + to_string(self.dy))
        print("W: " + to_string(self.width) + ", H: " + to_string(self.height))
        print("Bounces: " + to_string(self._bounces))
    }
    
    apply_force = { takes fx, fy, dt to
        self.dx = self.dx + fx * dt
        self.dy = self.dy + fy * dt
    }
    
    update = { takes dt to

        self.x = self.x + (self.dx * dt)
        self.y = self.y + (self.dy * dt)
        
        floor = SCREEN_HEIGHT - self.height
        
        if self.y >= floor {
            self.y = floor
        
            self._bounces = self._bounces + 1

            self.dy = self.dy * (-1)       
        } else {
            self.apply_force(0, 300, dt)
        }
        
    }
}

VeryPsuedoRandomGenerator = class {
    @init = {
        self._numbers = [53, 34536, 867, 92444, -4, 25672, 87531, 30, 1996, 458736, 77, -2, 095617, 242, 10, 593845, 32652, 097865, 561230, 468, 05, 73, 967, 723, 7, 1145329, -42]
        self._i = 0
    }
    
    next_number = {
        self._i = self._i + 1
        if self._i >= self._numbers.length() {
            self._i = 0
        }
        return math.abs(self._numbers[self._i])
    }
}

random = VeryPsuedoRandomGenerator()

Game = class {
    @init = { takes title to
        sdl.init(sdl.SDL_INIT_VIDEO)
        
        self.window = sdl.create_window(title, sdl.SDL_WINDOWPOS_UNDEFINED, sdl.SDL_WINDOWPOS_UNDEFINED, SCREEN_WIDTH, SCREEN_HEIGHT, 0)
        
        sdl.set_hint(sdl.SDL_HINT_RENDER_SCALE_QUALITY, "linear")
        self.renderer = sdl.create_renderer(self.window, -1, sdl.SDL_RENDERER_ACCELERATED)
        
        sdl.img_init(sdl.IMG_INIT_PNG)
        sdl.show_cursor(1)

        self.ball_texture = sdl.img_load_texture(self.renderer, "sdl_extension\pictures\ball.png")

        self.balls = []
        
        i = 0
        while i < 1 {
            self.spawn_ball()
            i = i + 1
        }

        self.running = true

        self.prepare_scene()
        
        self.draw()

        self.present_scene()
    }
    
    spawn_ball = {
        # print("Spawning")
        randnum = random.next_number()
        random_x = (math.min(SCREEN_WIDTH, randnum) / math.max(SCREEN_WIDTH, randnum)) * SCREEN_WIDTH
        texture = self.ball_texture
        self.balls[self.balls.length()] = Ball(random_x, 0, texture)
    }
    
    prepare_scene = {
        sdl.set_render_draw_color(self.renderer, 128, 230, 5, 255)
        sdl.render_clear(self.renderer)
    }
    
    present_scene = {
        sdl.render_present(self.renderer)
    }
    
    logic = { takes dt to
        if keyboard[sdl.SDL_SCANCODE_SPACE] {
            self.spawn_ball()
        }
        
        i = 0
        while i < self.balls.length() {
            ball = self.balls[i]
            
            ball.update(dt)
            
            i = i + 1
        }
    }
    
    draw = {
        i = 0
        while i < self.balls.length() {
            ball = self.balls[i]
            ball_details = []
            dest_rect = sdl.Rect(ball.x, ball.y, ball.width, ball.height)
            sdl.render_copy(self.renderer, ball.texture, nil, dest_rect)
            
            i = i + 1
        }
    }
    
    input = {
        event = sdl.poll_event()
        while (event != nil) {
            if event.type == sdl.SDL_QUIT {
                self.running = false
            } elsif event.type == sdl.SDL_KEYUP and event.repeat == 0 {
                keyboard[event.scancode] = false
            } elsif event.type == sdl.SDL_KEYDOWN and event.repeat == 0 {
                keyboard[event.scancode] = true
            }
            
            event = sdl.poll_event()
        }
    }
}

main = {
    game = Game("My Plane Game! :D")

    MAX_FPS = 50
    MAX_DT = 1 / MAX_FPS
    
    prev_time = 0
    curr_time = sdl.get_ticks() / 1000
    
    dbg_frame_count = 0
    dbg_frames_divided = 0
    
    while game.running {
    
        game.prepare_scene()
        
        game.input()
    
        prev_time = curr_time
        curr_time = sdl.get_ticks() / 1000
    
        frame_time = curr_time - prev_time
        
        dbg_x = 0
        while frame_time > 0 {
            dt = math.min(frame_time, MAX_DT)
            game.logic(dt)
            frame_time = frame_time - dt
            dbg_x = dbg_x + 1
        }
        if dbg_x > 0 {
            dbg_frames_divided = dbg_frames_divided + 1
        }
        
        game.draw()
        
        game.present_scene()
        
        dbg_frame_count = dbg_frame_count + 1
        
    }

    print("Balls: " + to_string(game.balls.length()))
    print("Frames: " + to_string(dbg_frame_count))
    print("dbg_frames_divided: " + to_string(dbg_frames_divided))
    print("Relation: " + to_string(dbg_frames_divided / dbg_frame_count))

}

main()